if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
    message(FATAL_ERROR "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt")
endif()

file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")

# Remove individual files
foreach(file ${files})
    message(STATUS "Uninstalling $ENV{DESTDIR}${file}")
    if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
        execute_process(
            COMMAND "@CMAKE_COMMAND@" -E remove "$ENV{DESTDIR}${file}"
            OUTPUT_VARIABLE rm_out
            RESULT_VARIABLE rm_retval
        )
        if(NOT "${rm_retval}" STREQUAL 0)
            message(FATAL_ERROR "Problem when removing $ENV{DESTDIR}${file}")
        endif()
    else()
        message(STATUS "File $ENV{DESTDIR}${file} does not exist.")
    endif()
endforeach()

# Only remove specific directories related to sealcrypt
set(SEALCRYPT_DIRS
    "/usr/local/include/sealcrypt"
    "/usr/local/lib/cmake/sealcrypt"
)

foreach(dir ${SEALCRYPT_DIRS})
    if(EXISTS "$ENV{DESTDIR}${dir}")
        message(STATUS "Removing directory: $ENV{DESTDIR}${dir}")
        execute_process(
            COMMAND "@CMAKE_COMMAND@" -E remove_directory "$ENV{DESTDIR}${dir}"
            OUTPUT_VARIABLE rm_out
            RESULT_VARIABLE rm_retval
        )
        if(NOT "${rm_retval}" STREQUAL 0)
            message(STATUS "Directory $ENV{DESTDIR}${dir} could not be removed")
        endif()
    endif()
endforeach()